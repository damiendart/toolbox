#!/usr/bin/perl -w

# Highlights tabs and trailing whitespace in source code and text files.
#
# This file was written by Damien Dart, <damiendart@pobox.com>. This is free
# and unencumbered software released into the public domain. For more
# information, please refer to the accompanying "UNLICENCE" file.

use strict;

use File::Basename;
use Getopt::Long;

sub printUsageInstructionsAndQuit
{
  print {$_[0] < 1 ? *STDOUT : *STDERR} <<USAGE;
"${\basename($0)}" highlights tabs and trailing whitespace in text files.
Usage: ${\basename($0)} [-?] [-tw] [- | FILE...]
Options:
  -?, --help                Print this message.
  -t, --ignore-tabs         Ignore tabs.
  -w, --ignore-whitespace   Ignore trailing whitespace.
USAGE
  exit($_[0]);
}

our ($opt_help, $opt_ignore_tabs, $opt_ignore_trailing_whitespace);
GetOptions("help|?", "ignore-tabs|t", "ignore-trailing-whitespace|w")
    or printUsageInstructionsAndQuit(2);
defined($opt_help) && printUsageInstructionsAndQuit(0);
while (<>) {
  print {*STDERR} "$ARGV:$.:$-[0]: One or more tabs.\n"
      if (/\t/ and not defined($opt_ignore_tabs));
  print {*STDERR} "$ARGV:$.:$-[0]: Trailing whitespace.\n" if
      (/[ \t]+$/ and not defined($opt_ignore_trailing_whitespace));
  close ARGV if eof;
}
