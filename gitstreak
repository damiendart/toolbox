#!/usr/bin/perl -w
# Calculates the current Git commit streak.
#
# This file was written by Damien Dart, <damiendart@pobox.com>. This is
# free and unencumbered software released into the public domain. For
# more information, please refer to the accompanying "UNLICENCE" file.

use strict;

use Date::Calc qw/Add_Delta_Days Delta_Days Today/;
use File::Basename;
use Getopt::Long;

sub printUsageInstructionsAndQuit
{
  print {$_[0] < 1 ? *STDOUT : *STDERR} <<USAGE;
"${\basename($0)}" calculates the current Git commit streak.
Usage: ${\basename($0)} [-?] [GIT-REPO]...
Options:
  -?, --help        Print this message and quit.
  -h, --html        Surround number of days with HTML "span" tags if
                    commit streak is longer than a week.
  -n, --no-newline  Suppress newlines from output.
  -q, --quiet       Suppress "ignoring directory" warning messages.
  -v, --verbose     Add "Current commit streak: " to output.
Notes:
  - The streak will break if a day has passed without commits.
USAGE
  exit($_[0]);
}

my @commit_dates;
my $streak = 0;
our ($opt_help, $opt_html, $opt_no_newline, $opt_quiet, $opt_verbose);
GetOptions("help|?", "html|h", "no-newline|n", "quiet|q", "verbose|v") or
    printUsageInstructionsAndQuit(2);
defined($opt_help) && printUsageInstructionsAndQuit(0);
foreach (map { glob } @ARGV) {
  if (system("((cd $_ && git rev-parse --git-dir) >/dev/null 2>&1)")) {
    print STDERR "Ignoring \"$_\": not a Git repository.\n" unless $opt_quiet;
    next;
  }
  push(@commit_dates, `(cd $_ && git log --pretty=format:%ad --date=short)`);
}
chomp(@commit_dates);
@commit_dates = reverse sort keys %{{ map { $_, 1 } @commit_dates}};
foreach(@commit_dates) {
  last if (Delta_Days(split(/-/), Add_Delta_Days(Today(), Delta_Days(
      split(/-/, $commit_dates[0]), Today()) != 0 ? -1 : 0)) != $streak);
  $streak++;
}
print((defined($opt_verbose) ? "Current commit streak: " : "") .
    (defined($opt_html) && $streak > 6 ? "<span class=\"fire\">" : "") .
    "$streak day" . ($streak == 1 ? "" : "s") .
    (defined($opt_html) && $streak > 6 ? "</span>" : "") .
    (defined($opt_no_newline) ? "" : "\n"));
